[[ -z "$LANG" ]] && export LANG=en_US.UTF-8
[[ -z "$LC_ALL" ]] && export LC_ALL=en_US.UTF-8

# from /etc/zprofile (macOS)
if [[ -x /usr/libexec/path_helper ]]; then
    eval $(/usr/libexec/path_helper -s)
fi

# autostart tmux if available
if [[ "$commands[tmux]" && -z "$VIM" && -z "$EMACS" && -z "$INSIDE_EMACS" && -z "$TMUX" ]]; then
    exec tmux -u new-session
fi

ZSHINE_DIR=~/.zshine

zshine_load=(
    ~/.zshrc.d/pre/*(N)
    "$ZSHINE_DIR/init.zsh"
    ~/.zshrc.d/post/*(N)
)

for _load in ${zshine_load[@]}; do
    source "$_load"
done
unset _load


# https://github.com/htr3n/zsh-config/blob/master/zlogin
# Execute code in the background to not affect the current session
(
    setopt LOCAL_OPTIONS EXTENDED_GLOB
    autoload -U zrecompile

    # Compile zcompdump, if modified, to increase startup speed.
    zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
    if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
        zrecompile -pq "$zcompdump"
    fi

    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshrc
    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zprofile
    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshenv

    # recompile all zsh or sh
    for f in $ZSHINE_DIR/**/*.*sh; do
        zrecompile -pq "$f"
    done
) &!
