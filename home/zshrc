[[ -z "$LANG" ]] && export LANG=en_US.UTF-8
[[ -z "$LC_ALL" ]] && export LC_ALL=en_US.UTF-8

ZSHINE_DIR=~/.zshine

# autostart tmux if available
if [[ "$commands[tmux]" && -z "$VIM" && -z "$EMACS" && -z "$INSIDE_EMACS" && -z "$TMUX" && $- == *i* ]]; then

    # attach to latest detached session if available
    detached_session=$(tmux list-session -F '#{session_name}:#{session_attached}' | grep ':0' | tail -1)
    if [[ -n "$detached_session" ]]; then
        exec tmux attach-session -t "${detached_session%:*}" \; display "re-attached session"
    else
        exec tmux -u new-session
    fi
fi

# switch to 256color if termite is not found
if [[ $TERM == xterm-termite && ! -e /usr/share/terminfo/x/xterm-termite ]]; then
    export TERM=xterm-256color
fi

zshine_load=(
    ~/.zshrc.d/pre/*(N)
    "$ZSHINE_DIR/init.zsh"
    ~/.zshrc.d/post/*(N)
)

for _load in ${zshine_load[@]}; do
    source "$_load"
done
unset _load


# https://github.com/htr3n/zsh-config/blob/master/zlogin
# Execute code in the background to not affect the current session
(
    setopt LOCAL_OPTIONS EXTENDED_GLOB
    autoload -U zrecompile

    # Compile zcompdump, if modified, to increase startup speed.
    zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
    if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
        zrecompile -pq "$zcompdump"
    fi
    # zcompile .zshrc
    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshrc
    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zprofile
    zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshenv
    # recompile all zsh or sh
    for f in $ZSHINE_DIR/**/*.*sh; do
        zrecompile -pq "$f"
    done
) &!
